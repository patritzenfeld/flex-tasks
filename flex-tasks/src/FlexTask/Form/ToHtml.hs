{-# language QuasiQuotes #-}
{-# language RecordWildCards #-}

module FlexTask.Form.ToHtml (
  getFormData,
  unsafeGetFormData,
  ) where


import qualified Yesod.Core.Unsafe      as Unsafe

import Control.Monad.Reader             (runReader)
import Data.Map                         (fromList)
import Data.IORef                       (readIORef, writeIORef)
import Data.Text                        (Text)
import Data.Tuple.Extra                 (thd3)
import System.IO.Unsafe                 (unsafePerformIO)
import System.Log.FastLogger            (defaultBufSize, newStdoutLoggerSet)
import Text.Blaze.Html.Renderer.String  (renderHtml)
import Yesod
import Yesod.Core.Types                 (HandlerData(..), HandlerFor(..), ghsIdent)
import Yesod.Default.Config2            (makeYesodLogger)

import FlexTask.Config.Types            (HtmlDict)
import FlexTask.Processing.Text         (supportedLanguages)
import FlexTask.Form.Types              (FlexForm(..), Handler, Rendered, Widget)



-- reset internal id generator to have same ids in all languages
resetIdentGen :: Handler ()
resetIdentGen = do
    x <- HandlerFor $ readIORef . handlerState
    HandlerFor $ flip writeIORef x {ghsIdent = 0} . handlerState


{- |
Extract a form from the environment inside an IO context.
The result is a tuple of field IDs and a map of language and internationalized html pairs.

This is an internal function used in the Autotool Flex-Task implementation.
You should never need to call this function yourself.
Use `unsafeGetFormData` instead.
-}
getFormData :: Rendered Widget -> IO ([Text], [[Text]], HtmlDict)
getFormData widget = do
    logger <- newStdoutLoggerSet defaultBufSize >>= makeYesodLogger
    Unsafe.fakeHandlerGetLogger
      appLogger
      FlexForm {appLogger = logger}
      writeHtml
  where
    writeHtml :: Handler ([Text], [[Text]], HtmlDict)
    writeHtml = case supportedLanguages of
      (l:ls) -> do
        (ids,names,first) <- withLang l
        rest <- traverse (fmap thd3 . withLang) ls
        return (ids,names, fromList $ first:rest)
      _ -> error "No supported languages found!"

    withLang :: Lang -> Handler ([Text], [[Text]], (Lang, String))
    withLang lang = setRequestLang lang $ do
      resetIdentGen
      (ids,names,wid) <- fst <$> runFormGet (runReader widget)
      content <- widgetToPageContent wid
      html <- withUrlRenderer [hamlet|
        ^{pageHead content}
        ^{pageBody content}|]
      return (ids, names, (lang, renderHtml html))


{- |
Extract a form from the environment.
The result is a tuple of field IDs and a map of language and internationalized html pairs.

Intended to be used for Autotool's task interface.

__Warning: This function employs `unsafePerformIO`!__
It should nevertheless be safe to use
as long as no lifted IO actions are executed while building the `Rendered` `Widget` argument.
This will always be the case for generic forms.
For custom forms, the user is responsible for making sure such calls are avoided or considered "safe".
-}
unsafeGetFormData :: Rendered Widget -> ([Text], [[Text]], HtmlDict)
unsafeGetFormData = unsafePerformIO . getFormData


-- Manipulate the request data to use a specific language.
setRequestLang :: Lang -> Handler a -> Handler a
setRequestLang lang HandlerFor{..} = do
  HandlerFor $ unHandlerFor . alterHandlerData
  where
    alterHandlerData hd@HandlerData{..} =
      hd{handlerRequest = handlerRequest{reqLangs = [lang]}}
